plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'dagger.hilt.android.plugin'
    id "org.jetbrains.kotlin.plugin.compose"
}

android {
    namespace 'io.github.kurramkurram.solitaire'
    compileSdk 36

    defaultConfig {
        applicationId "io.github.kurramkurram.solitaire"
        minSdk 26
        minSdk 26
        targetSdk 36
        versionCode 11
        versionName "1.5.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        resValue "string", "ad_application_id", "\"${properties.getProperty("AD_APPLICATION_ID")}\""
        resValue "string", "ad_unit_id", "\"${properties.getProperty("AD_UNIT_ID")}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "plan"
    productFlavors {
        pro {
            dimension "plan"
            applicationIdSuffix ".pro"
            buildConfigField "boolean", "IS_PRO", "true"
        }

        free {
            dimension "plan"
            buildConfigField "boolean", "IS_PRO", "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        buildConfig true
        dataBinding true
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.15"
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    mainClass.set("com.pinterest.ktlint.Main")
    classpath = configurations.ktlint
    args "src/**/*.kt", "--color", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
}

check.dependsOn ktlint

dependencies {

    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.1"
    implementation "androidx.fragment:fragment-ktx:1.8.8"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.9.1"

    // Room
    implementation "androidx.room:room-runtime:2.7.2"
    kapt "androidx.room:room-compiler:2.7.2"
    implementation "androidx.room:room-ktx:2.7.2"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:33.16.0')
    // Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx'
    // Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    // Authentication
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.android.gms:play-services-auth:21.3.0'
    // Storage
    implementation 'com.google.firebase:firebase-storage'
    // CloudMessaging
    implementation 'com.google.firebase:firebase-messaging'

    // oss
    implementation 'com.google.android.gms:play-services-oss-licenses:17.1.0'

    // font
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.1.1'

    // datastore
    implementation "androidx.datastore:datastore-preferences:1.1.7"

    // AdMob
    implementation 'com.google.android.gms:play-services-ads:24.4.0'

    // Review
    implementation 'com.google.android.play:review:2.0.2'
    implementation 'com.google.android.play:review-ktx:2.0.2'

    // MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // Jetpack Compose
    def composeBom = platform('androidx.compose:compose-bom:2025.06.01')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.compose.material:material'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'org.mockito:mockito-core:5.18.0'
    androidTestImplementation 'androidx.test:rules:1.6.1'

    // Hilt
    implementation "com.google.dagger:hilt-android:2.56.2"
    kapt "com.google.dagger:hilt-android-compiler:2.56.2"

    // ktlint
    ktlint 'com.pinterest:ktlint:0.50.0'
}