plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'dagger.hilt.android.plugin'
}

android {
    namespace 'io.github.kurramkurram.solitaire'
    compileSdk 34

    defaultConfig {
        applicationId "io.github.kurramkurram.solitaire"
        minSdk 26
        minSdk 26
        targetSdk 34
        versionCode 9
        versionName "1.4.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        resValue "string", "ad_application_id", "\"${properties.getProperty("AD_APPLICATION_ID")}\""
        resValue "string", "ad_unit_id", "\"${properties.getProperty("AD_UNIT_ID")}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "plan"
    productFlavors {
        pro {
            dimension "plan"
            applicationIdSuffix ".pro"
            buildConfigField "boolean", "IS_PRO", "true"
        }

        free {
            dimension "plan"
            buildConfigField "boolean", "IS_PRO", "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.4"
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    mainClass.set("com.pinterest.ktlint.Main")
    classpath = configurations.ktlint
    args "src/**/*.kt", "--color", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
}

check.dependsOn ktlint

dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0"
    implementation "androidx.fragment:fragment-ktx:1.6.2"

    // Navigation
    implementation "androidx.navigation:navigation-fragment:2.7.6"
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.6"

    // Room
    implementation "androidx.room:room-runtime:2.6.1"
    kapt "androidx.room:room-compiler:2.6.1"
    implementation "androidx.room:room-ktx:2.6.1"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:32.7.1')
    // Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx'
    // Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    // Authentication
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    // Storage
    implementation 'com.google.firebase:firebase-storage'
    // CloudMessaging
    implementation 'com.google.firebase:firebase-messaging'

    // oss
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'

    // font
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'

    // datastore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // AdMob
    implementation 'com.google.android.gms:play-services-ads:22.6.0'

    // Review
    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.google.android.play:review-ktx:2.0.1'

    // MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // Jetpack Compose
    def composeBom = platform('androidx.compose:compose-bom:2023.10.01')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.compose.material:material'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'org.mockito:mockito-core:2.28.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    // Hilt
    implementation "com.google.dagger:hilt-android:2.50"
    kapt "com.google.dagger:hilt-android-compiler:2.50"

    // ktlint
    ktlint 'com.pinterest:ktlint:0.43.1'
}